apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

// ext is a gradle closure allowing the declaration of global properties
ext {
    PUBLISH_GROUP_ID = 'com.android_space'
    PUBLISH_ARTIFACT_ID = 'viewplanetsactivity'
    PUBLISH_VERSION = '1.0.2'
}

def localReleaseDest = "/Users/kristina/localAndroidRepos"
def siteUrl = 'https://github.com/kristina-hager/android-planetary-viewer'
def gitUrl = 'https://github.com/kristina-hager/android-planetary-viewer.git'

android {
    compileSdkVersion 21
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    maven {
        url  "http://dl.bintray.com/kristina-hager/kh-examples"
    }
    jcenter()
}

dependencies {
    compile group: 'example.com', name: 'planetary-system', version: '1.0.0'
    compile 'com.android.support:appcompat-v7:22.2.1'
}

//javadocs and sources jar code from:
//http://blog.blundell-apps.com/locally-release-an-android-library-for-jcenter-or-maven-central-inclusion/
task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

//publish to Maven repo on local filesystem
uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = PUBLISH_GROUP_ID
        pom.artifactId = PUBLISH_ARTIFACT_ID
        pom.version = PUBLISH_VERSION
        // Add other pom properties here if you want (developer details / licenses)
        repository(url: "file://${localReleaseDest}")
    }
}
//require :build tasks such as :test,:assemble before upload archives
tasks.uploadArchives.dependsOn(build)

//helpful hints:
//https://github.com/bintray/bintray-examples/blob/master/gradle-aar-example/build.gradle#L115
bintray {
    user =  System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    publications = ['MyPublication']
    pkg {
        repo = 'kh-examples'
        name = PUBLISH_ARTIFACT_ID
        desc = 'viewplanetsactivity aar publishing example'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['aar', 'android', 'example']
        publicDownloadNumbers = true
        version {
            name = PUBLISH_VERSION
        }
    }
}

//helpful hints: https://github.com/lecho/hellocharts-android/blob/master/hellocharts-library/build.gradle
//adding maven information
publishing {
    publications {
        //name 'MyPublication' corresponds to 'publications' setting in bintray section
        MyPublication(MavenPublication) {
            artifact "${project.buildDir}/outputs/aar/${project.name}-release.aar"
            groupId PUBLISH_GROUP_ID
            artifactId PUBLISH_ARTIFACT_ID
            version PUBLISH_VERSION

            //add sources and javadoc jars to upload
            artifact(androidSourcesJar)
            artifact(androidJavadocsJar)
        }
    }
}
//again, add a dependency on :build to :bintrayUpload
tasks.bintrayUpload.dependsOn(build)